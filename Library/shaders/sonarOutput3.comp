/*    
    Copyright (c) 2020 Patryk Cieslak. All rights reserved.

    This file is a part of Stonefish.

    Stonefish is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    Stonefish is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
*/

//N_BINS - number of histogram bins
//N_HORI_BEAM_SAMPLES - number of horizontal beam samples

layout(local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

// Two inputs, linear range and recorded intensity.
layout(rg32f) uniform image2D sonarInput;

// Two outputs - intensity for each bin/vertical beam sample and number of hits
layout(rg32f) uniform image2D sonarHist;

uniform vec3 range; //min, max, step

vec2 binHistogram[N_BINS];

void main()
{
    uvec2 inDim = imageSize(sonarInput).xy; //nBeamSamples.x * nBeamSamples.y 
    
    if(gl_GlobalInvocationID.x < inDim.y) //For each vertical row
    {
        for(int i=0; i<N_BINS; ++i) //Initialize histogram buffer
            binHistogram[i] = vec2(0.0); 

        ivec2 sampleCoord;
        sampleCoord.y = int(gl_GlobalInvocationID.x);
        float vFrac2 = (float(gl_GlobalInvocationID.x)/float(int(inDim.y)-1)-0.5)*2.0;
        vFrac2 *= vFrac2;

        for(int i=0; i<N_HORI_BEAM_SAMPLES; ++i) //For each horizontal beam sample
        {            
            sampleCoord.x = i;

            //Get sample value
            vec2 rangeIntensity = imageLoad(sonarInput, sampleCoord).xy;
            if(rangeIntensity.x < range.x || rangeIntensity.x >= range.y) //Outside valid range?
                continue;

            //Compute bin and accumulate intensity data
            float hFrac2 = (float(i)/float(N_HORI_BEAM_SAMPLES-1)-0.5)*2.0;
            hFrac2 *= hFrac2;
            uint bin = uint(floor((rangeIntensity.x - range.x)/range.z));
            float beamPattern = clamp(1.0 - (hFrac2 + vFrac2)/2.0, 0.0, 1.0);
            binHistogram[bin].x += rangeIntensity.y * beamPattern;
            binHistogram[bin].y += 1.0;
        }

        //Store bin values
        for(int i=0; i<N_BINS; ++i)
            imageStore(sonarHist, ivec2(sampleCoord.y, i), vec4(binHistogram[i].xy, 0.0, 0.0));
    }
}